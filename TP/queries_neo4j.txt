// Média de salários por departamentos
Match(e:EMPLOYEE)-[w:WORKS_IN]->(d:DEPARTMENT)
Return d.DEPARTMENT_NAME ,round(avg(toInteger(e.SALARY)),2) as media
order by media desc 

// Top 5 empregados com o contrato ativo mais duradouro em cada região
MATCH (e:EMPLOYEE)-[:WORKS_IN]->(d:DEPARTMENT)-[:LOCATED_IN]->(l:LOCATION)-[:LOCATED_IN]->(c:COUNTRY)-[:LOCATED_IN]->(r:REGION)
WITH e.FIRST_NAME+' '+e.LAST_NAME AS Nome, e.HIRE_DATE AS Datas, r 
ORDER BY e.HIRE_DATE, Nome
RETURN r.REGION_NAME AS Região, collect(Nome)[..5] As Nomes, collect(Datas)[..5] AS Datas_Início

// Emprego antigo em que o empregado com ID 176 faturou mais
MATCH (e:EMPLOYEE)-[r:PRACTICED]->(j:JOB)
WHERE e.EMPLOYEE_ID = '176'
RETURN j.JOB_TITLE as Emprego, 
	  r.START_DATE as Data_Inicial, 
    r.END_DATE as Data_Final, 
    duration.inMonths(date(replace(r.START_DATE,".","-")),
    date(REPLACE(r.END_DATE,".","-"))).months*(toInteger(j.MAX_SALARY)) AS Faturado
ORDER BY Faturado DESC
LIMIT 1

// Empregados no 4º nivel da cadeia de supervisão
MATCH (e:EMPLOYEE)-[:SUPERVISED_BY]->(:EMPLOYEE)-[:SUPERVISED_BY]->(:EMPLOYEE)-[:SUPERVISED_BY]->(:EMPLOYEE)
RETURN e.FIRST_NAME+' '+e.LAST_NAME AS Nome
ORDER BY Nome

// Criar relacionamentos RESIDES_IN de trabalhador para país, para todos os trabalhadores que estão no 3º nível da cadeia de supervisão e são shipping clerks ou sales representative
MATCH (e:EMPLOYEE)-[:WORKS_IN]->(:DEPARTMENT)-[:LOCATED_IN]->(:LOCATION)-[:LOCATED_IN]->(c:COUNTRY)
MATCH (j:JOB)<-[:PRACTICES]-(e:EMPLOYEE)-[:SUPERVISED_BY]->(:EMPLOYEE)-[:SUPERVISED_BY]->(:EMPLOYEE)
WHERE j.JOB_ID = 'SH_CLERK' OR j.JOB_ID = 'SA_REP'
MERGE (e)-[:RESIDES_IN]->(c)
RETURN e,c
